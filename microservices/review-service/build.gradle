plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'

    // The Checkstyle plugin performs quality checks on your projectâ€™s Java source files using Checkstyle and
    // generates reports from these checks.
    alias libs.plugins.checkstyle

    // Formatting codes
    alias libs.plugins.spotless

    // Spring boot related plugins
    alias libs.plugins.spring.boot
    alias libs.plugins.dependency.management
}

ext {
    springCloudVersion = '2024.0.0'
}

dependencies {
    implementation project(':microservices:api')
    implementation project(':microservices:util')

    implementation libs.guava

    implementation libs.spring.boot.starter.actuator
    implementation libs.spring.boot.starter.webflux

    // To avoid the following error message on Apple silicon (for details, see https://github.com/netty/netty/issues/11693):
    // Unable to load io.netty.resolver.dns.macos.MacOSDnsServerAddressStreamProvider, fallback to system defaults. This may result in incorrect DNS resolutions on MacOS.
    implementation group: 'io.netty', name: 'netty-resolver-dns-native-macos', version: '4.1.115.Final', classifier: 'osx-aarch_64'

    implementation libs.mapstruct
    implementation libs.spring.data.mysql
    implementation libs.mysql.connector
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    compileOnly libs.mapstruct.annotation.processor
    annotationProcessor libs.mapstruct.annotation.processor
    testAnnotationProcessor libs.mapstruct.annotation.processor

    testImplementation libs.spring.boot.starter.test
    testImplementation libs.reactor.test

    testImplementation libs.spring.testcontainers
    testImplementation libs.testcontainers.core
    testImplementation libs.testcontainers.junit
    testImplementation libs.testcontainers.mysql
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

base {
    archivesName = 'review-service'
}

version = '0.0.1'
group = 'com.github.khshourov.microservices.core'

application {
    // Define the main class for the application.
    mainClass = 'com.github.khshourov.microservices.core.review.ReviewServiceApplication'
}

checkstyle {
    toolVersion = '10.20.1'
    // Directly copied from: https://github.com/checkstyle/checkstyle/blob/master/src/main/resources/google_checks.xml
    configFile = file("${project.rootDir}/checkstyle.xml")
}

// Directly copied from: https://github.com/diffplug/spotless/tree/main/plugin-gradle#java
spotless {
    java {
        // Use the default importOrder configuration
        importOrder()
        // optional: you can specify import groups directly
        // note: you can use an empty string for all the imports you didn't specify explicitly, '|' to join group without blank line, and '\\#` prefix for static imports
        // importOrder('java|javax', 'com.acme', '', '\\#com.acme', '\\#')
        // optional: instead of specifying import groups directly you can specify a config file
        // export config file: https://github.com/diffplug/spotless/blob/main/ECLIPSE_SCREENSHOTS.md#creating-spotlessimportorder
        // importOrderFile('eclipse-import-order.txt') // import order file as exported from eclipse

        removeUnusedImports()

        // Cleanthat will refactor your code, but it may break your style: apply it before your formatter
        cleanthat()

        googleJavaFormat()

        formatAnnotations()  // fixes formatting of type annotations

        // licenseHeader '/* (C) $YEAR */' // or licenseHeaderFile
    }
}

testing {
    suites {
        test {
            useJUnitJupiter('5.10.3')
        }
    }
}